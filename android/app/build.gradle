buildscript {

    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'com.jakewharton.sdkmanager:gradle-plugin:0.12.0'
        classpath 'com.jakewharton.hugo:hugo-plugin:1.1.0'
        classpath 'com.stanfy.spoon:spoon-gradle-plugin:0.10.0'
    }
}

apply plugin: 'android-sdk-manager'
apply plugin: 'com.android.application'
apply plugin: 'spoon'
//apply plugin: 'hugo'
apply plugin: "sonar-runner"



repositories {
    jcenter()
    maven { url 'https://github.com/Goddchen/mvn-repo/raw/master/' }
    mavenCentral()
    mavenLocal()
    maven { url 'http://mente.github.io/facebook-api-android-aar' }
}

def gitSha = 'git rev-parse --short HEAD'.execute([], project.rootDir).text.trim()
def buildTime = new Date().format("yyyy-MM-dd'T'HH:mm'Z'", TimeZone.getTimeZone("UTC"))

android {
    compileSdkVersion 21
    buildToolsVersion "21.0.2"

    defaultConfig {
        applicationId "gm.mobi.android.goles5"
        minSdkVersion 14
        targetSdkVersion 21


        versionCode System.getenv("BUILD_NUMBER") as Integer ?: 0
        versionName "0.1"
        project.ext.set("archivesBaseName", "Shootr-" + versionName + "-" + versionCode);

        resConfigs "es", "en"
        resConfigs "hdpi","xhdpi","xxhdpi"

        buildConfigField "String", "GIT_SHA", "\"${gitSha}\""
        buildConfigField "String", "BUILD_TIME", "\"${buildTime}\""

        testInstrumentationRunner "com.google.android.apps.common.testing.testrunner.GoogleInstrumentationTestRunner"
    }

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/ASL2.0'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/license.txt'
        exclude 'LICENSE.txt'
    }

    signingConfigs{
        release{
            storeFile file('../certs/release.keystore')
            //TODO use real release keystore
            storePassword "android"
            keyAlias "androiddebugkey"
            keyPassword "android"
        }

        debug{
            storeFile file('../certs/debug.keystore')
        }
    }

    buildTypes {
        //TODO hace falta?
        lintOptions {
            abortOnError false
            disable 'InvalidPackage'
        }
        debug {
            runProguard false
            applicationIdSuffix ".debug"
            signingConfig signingConfigs.debug
            signingConfig signingConfigs.debug

            buildConfigField "Boolean", "USE_FILE_LOGGER", "true"
        }
        release {
            runProguard false //TODO configurarlo y activarlo
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release

            buildConfigField "Boolean", "USE_FILE_LOGGER", "false"
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    // Google libraries
    compile 'com.android.support:appcompat-v7:21.0.0'
    compile 'com.google.android.gms:play-services:6.1.11'

    compile 'com.fasterxml.jackson.core:jackson-databind:2.2.4'
    compile 'com.fasterxml.jackson.core:jackson-core:2.2.4'

    compile 'com.squareup.dagger:dagger:1.2.2'
    provided 'com.squareup.dagger:dagger-compiler:1.2.2'

    compile 'com.squareup:otto:1.3.4'
    compile 'com.squareup.picasso:picasso:2.3.4'
    //compile 'com.squareup.okhttp:okhttp:2.0.0' //TODO meter por gradle cuando salga la versiÃ³n corregida de Android L: https://github.com/square/okhttp/issues/967

    compile 'com.jakewharton:butterknife:5.1.2'
    compile 'com.jakewharton.timber:timber:2.4.1'
    debugCompile 'com.jakewharton.scalpel:scalpel:1.1.1'

    compile 'com.path:android-priority-jobqueue:1.1.2'
    compile 'com.github.castorflex.smoothprogressbar:library:0.5.2'
    compile 'de.hdodenhof:circleimageview:1.2.0'
    compile 'com.github.dmytrodanylyk.circular-progress-button:library:1.1.3'

    compile 'es.oneoctopus.swiperefreshlayoutoverlay:library:3.0.0'

    //TODO Should be androidTestCompile, but doesn't work, probably due to a bug in Gradle/AS: http://stackoverflow.com/questions/22246183/android-studio-doesnt-recognize-espresso-classes
    provided 'com.jakewharton.espresso:espresso:1.1-r3'
    androidTestCompile ('com.jakewharton.espresso:espresso:1.1-r3')
      {
          exclude group: 'com.squareup.dagger'
          //exclude group: 'org.hamcrest', module: 'hamcrest-core'
          //exclude group: 'org.hamcrest', module: 'hamcrest-library'
          //exclude group: 'org.hamcrest', module: 'hamcrest-integration'
          //exclude group: 'com.google.guava'
          exclude group: 'javax.inject'
          exclude group: 'javax.annotation'
          //exclude group: 'com.google.code.findbugs'
      }
    androidTestCompile 'com.squareup.spoon:spoon-client:1.1.0'
}

spoon {
    debug = true
    failIfNoDeviceConnected = true
    //className = 'gm.mobi.android.ui.activities.registro.WelcomeLoginActivityTest'
}

sonarRunner {
    sonarProperties {
        property "sonar.host.url", "http://development.golesmessenger.com:9090"
        property "sonar.jdbc.url", "jdbc:mysql://localhost:3306/sonar?useUnicode=true&amp;characterEncoding=utf8"
        property "sonar.jdbc.driverClassName", "com.mysql.jdbc.Driver"
        property "sonar.jdbc.username", "sonar"
        property "sonar.jdbc.password", "sonar"
        property "sonar.login", "admin"
        property "sonar.password", "admin"
        properties["sonar.sources"] = android.sourceSets.main.java.srcDirs
        //properties["sonar.tests"] = android.sourceSets.instrumentTest.java.srcDirs
        //property "sonar.junit.reportsPath","build/instrumentTest-results/connected/"
        properties["sonar.binaries"] = file("build/classes/debug")
    }
}

//
// Remove useless packages from Google Play Services library
//

def toCamelCase(String string) {
    String result = ""
    string.findAll("[^\\W]+") { String word ->
        result += word.capitalize()
    }
    return result
}

afterEvaluate { project ->
    Configuration runtimeConfiguration = project.configurations.getByName('compile')
    ResolutionResult resolution = runtimeConfiguration.incoming.resolutionResult
    // Forces resolve of configuration
    ModuleVersionIdentifier module = resolution.getAllComponents().find { it.moduleVersion.name.equals("play-services") }.moduleVersion

    String prepareTaskName = "prepare${toCamelCase("${module.group} ${module.name} ${module.version}")}Library"
    File playServiceRootFolder = project.tasks.find { it.name.equals(prepareTaskName) }.explodedDir

    Task stripPlayServices = project.tasks.create(name: 'stripPlayServices', group: "Strip") {
        inputs.files new File(playServiceRootFolder, "classes.jar")
        outputs.dir playServiceRootFolder
        description 'Strip useless packages from Google Play Services library to avoid reaching dex limit'

        doLast {
            copy {
                from(file(new File(playServiceRootFolder, "classes.jar")))
                into(file(playServiceRootFolder))
                rename { fileName ->
                    fileName = "classes_orig.jar"
                }
            }
            tasks.create(name: "stripPlayServices" + module.version, type: Jar) {
                destinationDir = playServiceRootFolder
                archiveName = "classes.jar"
                from(zipTree(new File(playServiceRootFolder, "classes_orig.jar"))) {
                    exclude "com/google/ads/**"
                    exclude "com/google/android/gms/drive/**"
                    exclude "com/google/android/gms//**"
                    exclude "com/google/android/gms/games/**"
                    exclude "com/google/android/gms/drive/**"
                    exclude "com/google/android/gms/ads/**"
                    exclude "com/google/android/gms/wearable/**"
                    exclude "com/google/android/gms/wallet/**"
                    exclude "com/google/android/gms/panorama/**"

                    //google the play services packages and remove your useless
                }
            }.execute()
            delete {
                delete (file(new File(playServiceRootFolder, "classes_orig.jar")))
            }
        }
    }

    project.tasks.findAll { it.name.startsWith('prepare') && it.name.endsWith('Dependencies') }.each { Task task ->
        task.dependsOn stripPlayServices
    }
}