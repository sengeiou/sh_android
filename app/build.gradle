buildscript {

    repositories {
        mavenCentral()
        maven { url 'http://download.crashlytics.com/maven' }
    }

    dependencies {
        classpath 'com.jakewharton.sdkmanager:gradle-plugin:0.12.0'
        classpath 'com.jakewharton.hugo:hugo-plugin:1.1.0'
        classpath 'com.stanfy.spoon:spoon-gradle-plugin:0.10.0'
        classpath 'com.crashlytics.tools.gradle:crashlytics-gradle:1.+'
    }
}

apply plugin: 'android-sdk-manager'
apply plugin: 'com.android.application'
apply plugin: 'crashlytics'
//apply plugin: 'spoon'
//apply plugin: 'hugo'
apply plugin: "sonar-runner"
apply plugin: 'maven'



repositories {
    maven { url 'https://github.com/Goddchen/mvn-repo/raw/master/' }
    mavenCentral()
    mavenLocal()
    maven { url 'http://mente.github.io/facebook-api-android-aar' }
    maven { url 'http://download.crashlytics.com/maven' }
}

def gitSha = 'git rev-parse --short HEAD'.execute([], project.rootDir).text.trim()
def buildTime = new Date().format("yyyy-MM-dd'T'HH:mm'Z'", TimeZone.getTimeZone("UTC"))

android {
    compileSdkVersion 21
    buildToolsVersion "21.1.1"

    defaultConfig {
        applicationId "com.shootr.android"
        minSdkVersion 14
        targetSdkVersion 21


        versionCode System.getenv("BUILD_NUMBER") as Integer ?: 0
        versionName "0.1."+versionCode+"-SNAPSHOT"
        project.ext.set("archivesBaseName", "Shootr-" + versionName);

        resConfigs "es", "en"
        resConfigs "hdpi","xhdpi","xxhdpi"

        buildConfigField "String", "GIT_SHA", "\"${gitSha}\""
        buildConfigField "String", "BUILD_TIME", "\"${buildTime}\""

        testInstrumentationRunner "com.google.android.apps.common.testing.testrunner.GoogleInstrumentationTestRunner"
    }

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/ASL2.0'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/license.txt'
        exclude 'LICENSE.txt'
    }

    signingConfigs{
        release{
            storeFile file('../certs/release.keystore')
            //TODO use real release keystore
            storePassword "android"
            keyAlias "androiddebugkey"
            keyPassword "android"
        }

        debug{
            storeFile file('../certs/debug.keystore')
        }
    }

    lintOptions {
        abortOnError false
        disable 'InvalidPackage'
    }

    buildTypes {
        debug {
            minifyEnabled false
            applicationIdSuffix ".debug"
            signingConfig signingConfigs.debug
            signingConfig signingConfigs.debug

            buildConfigField "Boolean", "USE_FILE_LOGGER", "true"
        }
        release {
            minifyEnabled false //TODO configurarlo y activarlo
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release

            buildConfigField "Boolean", "USE_FILE_LOGGER", "false"
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
}

dependencies {
    def domainLayer = project(':domain')
    compile domainLayer

    compile fileTree(dir: 'libs', include: ['*.jar'])

    compile 'com.android.support:appcompat-v7:21.0.2'
    compile 'com.android.support:recyclerview-v7:21.0.0'
    compile 'com.google.android.gms:play-services-base:6.5.87'

    compile 'com.fasterxml.jackson.core:jackson-databind:2.2.4'
    compile 'com.fasterxml.jackson.core:jackson-core:2.2.4'

    compile 'com.squareup.dagger:dagger:1.2.2'
    provided 'com.squareup.dagger:dagger-compiler:1.2.2'

    compile 'com.squareup:otto:1.3.4'
    compile 'com.squareup.picasso:picasso:2.4.0'
    //compile 'com.squareup.okhttp:okhttp:2.0.0' //TODO meter por gradle cuando salga la versi√≥n corregida de Android L: https://github.com/square/okhttp/issues/967

    compile 'com.jakewharton:butterknife:5.1.2'
    compile 'com.jakewharton.timber:timber:2.4.1'
    debugCompile 'com.jakewharton.scalpel:scalpel:1.1.1'

    compile 'com.path:android-priority-jobqueue:1.1.2'
    compile 'com.github.castorflex.smoothprogressbar:library:0.5.2'
    compile 'de.hdodenhof:circleimageview:1.2.0'
    compile 'com.github.dmytrodanylyk.circular-progress-button:library:1.1.3'

    compile 'com.google.guava:guava:18.0'

    compile 'joda-time:joda-time:2.3'

    compile 'com.cocosw:bottomsheet:0.5@aar'

    compile 'com.github.chrisbanes.photoview:library:1.2.3'

    compile 'com.melnykov:floatingactionbutton:1.1.0'

    compile 'com.squareup.phrase:phrase:1.0.3'

    compile 'com.github.flavienlaurent.datetimepicker:library:0.0.2'

    //TODO Should be androidTestCompile, but doesn't work, probably due to a bug in Gradle/AS: http://stackoverflow.com/questions/22246183/android-studio-doesnt-recognize-espresso-classes
    provided 'com.jakewharton.espresso:espresso:1.1-r3'
    androidTestCompile ('com.jakewharton.espresso:espresso:1.1-r3')
      {
          exclude group: 'com.squareup.dagger'
          //exclude group: 'org.hamcrest', module: 'hamcrest-core'
          //exclude group: 'org.hamcrest', module: 'hamcrest-library'
          //exclude group: 'org.hamcrest', module: 'hamcrest-integration'
          exclude group: 'com.google.guava'
          exclude group: 'javax.inject'
          exclude group: 'javax.annotation'
          //exclude group: 'com.google.code.findbugs'
      }
    androidTestCompile 'com.squareup.spoon:spoon-client:1.1.0'
    compile 'com.crashlytics.android:crashlytics:1.+'
}

sonarRunner {
    sonarProperties {
        property "sonar.host.url", "http://sonar.fav24.net"
        property "sonar.jdbc.url", "jdbc:mysql://gm-ci-environment.cttjtyko7rpp.eu-west-1.rds.amazonaws.com:3306/sonar?useUnicode=true&amp;characterEncoding=utf8"
        property "sonar.jdbc.driverClassName", "com.mysql.jdbc.Driver"
        property "sonar.jdbc.username", "sonar"
        property "sonar.jdbc.password", "sonar"
        property "sonar.login", "admin"
        property "sonar.password", "14041981"
        property "sonar.projectName", "Shootr Android"
        properties["sonar.sources"] = android.sourceSets.main.java.srcDirs
    }
}

uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: "http://nexus.fav24.net/content/repositories/snapshots/") {
                authentication(userName: "deployment", password: "deployment123")
            }
            pom.version = android.defaultConfig.versionName
            pom.artifactId = 'Shootr'
            pom.groupId = "com.shooter.android"
            uniqueVersion = false
        }
    }
}