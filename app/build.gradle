buildscript {

    repositories {
        mavenCentral()
        maven { url 'http://download.crashlytics.com/maven' }
    }

    dependencies {
        classpath 'com.jakewharton.sdkmanager:gradle-plugin:0.12.0'
        classpath 'com.jakewharton.hugo:hugo-plugin:1.1.0'
        classpath 'com.crashlytics.tools.gradle:crashlytics-gradle:1.14.7'
    }
}

apply plugin: 'android-sdk-manager'
apply plugin: 'com.android.application'
apply plugin: 'crashlytics'
apply plugin: 'hugo'
apply plugin: "sonar-runner"
apply plugin: 'maven'


repositories {
    maven { url 'https://github.com/Goddchen/mvn-repo/raw/master/' }
    mavenCentral()
    mavenLocal()
    maven { url 'http://mente.github.io/facebook-api-android-aar' }
    maven { url 'http://download.crashlytics.com/maven' }
    jcenter()
}

def gitSha = 'git rev-parse --short HEAD'.execute([], project.rootDir).text.trim()
def gitBranch = 'git rev-parse --abbrev-ref HEAD'.execute([], project.rootDir).text.trim()

def versionMajor = 0
def versionMinor = 1
def versionRevision = 21
def databaseVersion = 8
def isSnapshot = System.getenv("SNAPSHOT") as Boolean ?: true

android {
    compileSdkVersion 22
    buildToolsVersion "22.0.1"

    defaultConfig {
        applicationId "com.shootr.android"
        minSdkVersion 14
        targetSdkVersion 22


        versionCode versionMajor * 1000000 + versionMinor * 1000 + versionRevision
        versionName versionMajor + "." + versionMinor + "." + versionRevision
        versionName isSnapshot ? versionName + "-SNAPSHOT" : versionName
        archivesBaseName = "Shootr-" + versionName

        resConfigs "es", "en"
        resConfigs "hdpi", "xhdpi", "xxhdpi"

        buildConfigField "String", "GIT_SHA", "\"${gitSha}\""
        buildConfigField "String", "GIT_BRANCH", "\"${gitBranch}\""

        testInstrumentationRunner "com.google.android.apps.common.testing.testrunner.GoogleInstrumentationTestRunner"
    }

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/ASL2.0'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/license.txt'
        exclude 'LICENSE.txt'
    }

    signingConfigs {
        release {
            storeFile file('../certs/release.keystore')
            def (releaseKeystorePassword, releaseKeyAlias, releaseKeyPassword) = signingConfig()
            storePassword releaseKeystorePassword
            keyAlias releaseKeyAlias
            keyPassword releaseKeyPassword
        }

        debug {
            storeFile file('../certs/debug.keystore')
        }
    }

    lintOptions {
        abortOnError false
        disable 'InvalidPackage'
    }

    buildTypes {
        debug {
            minifyEnabled false
            applicationIdSuffix ".debug"
            signingConfig signingConfigs.debug
            signingConfig signingConfigs.debug

            buildConfigField "String", "API_ENDPOINT_BASE", "\"http://tst.api.shootr.com/v1\""
            buildConfigField "String", "RESOURCES_ENDPOINT_BASE", "\"http://resources.tst.shootr.com/\""
            buildConfigField "Integer", "DATABASE_VERSION", databaseVersion.toString()
        }
        release {
            minifyEnabled false //TODO configurarlo y activarlo
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release

            buildConfigField "String", "API_ENDPOINT_BASE", "\"http://api.shootr.com/v1\""
            buildConfigField "String", "RESOURCES_ENDPOINT_BASE", "\"http://resources.pro.shootr.com/\""
            buildConfigField "Integer", "DATABASE_VERSION", databaseVersion.toString()
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
}

dependencies {
    def domainLayer = project(':domain')
    compile domainLayer

    compile project(':okresponsefaker')

    compile fileTree(dir: 'libs', include: ['*.jar'])

    compile 'com.android.support:appcompat-v7:22.2.0'
    compile 'com.android.support:design:22.2.0'
    compile 'com.android.support:recyclerview-v7:22.2.0'
    compile 'com.google.android.gms:play-services-base:6.5.87'
    compile 'com.android.support:support-annotations:22.2.0'

    compile 'com.fasterxml.jackson.core:jackson-databind:2.2.4'
    compile 'com.fasterxml.jackson.core:jackson-core:2.2.4'

    compile 'com.squareup.dagger:dagger:1.2.2'
    provided 'com.squareup.dagger:dagger-compiler:1.2.2'

    compile 'com.squareup:otto:1.3.4'
    compile 'com.squareup.picasso:picasso:2.5.0'
    compile 'com.squareup.okhttp:okhttp:2.2.0'

    compile 'com.jakewharton:butterknife:7.0.1'
    compile 'com.jakewharton.timber:timber:2.4.1'
    debugCompile 'com.jakewharton.scalpel:scalpel:1.1.1'

    compile 'com.path:android-priority-jobqueue:1.1.2'
    compile 'com.github.castorflex.smoothprogressbar:library:0.5.2'
    compile 'de.hdodenhof:circleimageview:1.2.0'
    compile 'com.github.dmytrodanylyk.circular-progress-button:library:1.1.3'

    compile 'com.squareup.retrofit:retrofit:1.9.0'
    compile 'com.squareup.retrofit:converter-jackson:1.9.0'
    debugCompile 'com.squareup.retrofit:retrofit-mock:1.9.0'

    compile 'joda-time:joda-time:2.3'

    compile 'com.cocosw:bottomsheet:0.6@aar'

    compile 'com.github.chrisbanes.photoview:library:1.2.3'

    compile 'com.melnykov:floatingactionbutton:1.1.0'

    compile 'com.squareup.phrase:phrase:1.0.3'

    compile 'com.github.flavienlaurent.datetimepicker:library:0.0.2'

    compile 'com.facebook.android:facebook-android-sdk:4.1.0'

    compile 'com.facebook.stetho:stetho:1.1.0'
    compile 'com.facebook.stetho:stetho-okhttp:1.1.0'

    compile 'com.crashlytics.android:crashlytics:1.1.13'

    compile 'cat.ereza:customactivityoncrash:1.3.0'

    debugCompile 'com.github.pedrovgs:lynx:1.4'

    testCompile 'junit:junit:4.12'
    testCompile "org.mockito:mockito-core:1.9.5"
    testCompile 'org.assertj:assertj-core:1.7.0'
    androidTestCompile('com.squareup.assertj:assertj-android:1.0.0') {
        exclude group: 'com.android.support'
    }
}

/**
 * Tries to read signing values form a properties file, and fallbacks to environment variables if they are not found
 * @return keystorePassword , keyAlias, keyPassword
 */
def signingConfig() {
    signingConfigFromFile() ?: signinConfigFromEnvironment()
}

/**
 * Reads signing values form a environment variables
 * @return keystorePassword , keyAlias, keyPassword
 */
def signinConfigFromEnvironment() {
    println '-> signinConfigFromEnvironment'
    [System.getenv('STORE_PASSWORD'), System.getenv('KEY_ALIAS'), System.getenv('KEY_PASSWORD')]
}

/**
 * Reads signing values form a properties file
 * @return keystorePassword , keyAlias, keyPassword
 */
def signingConfigFromFile() {
    println '-> signingConfigFromFile'
    def Properties props = new Properties()
    def propFile = file('signing.properties')

    if (propFile.canRead()) {
        props.load(new FileInputStream(propFile))

        if (props != null &&
          props.containsKey('STORE_PASSWORD') &&
          props.containsKey('KEY_ALIAS') &&
          props.containsKey('KEY_PASSWORD')) {

            def keystorePassword = props['STORE_PASSWORD']
            def keyAlias = props['KEY_ALIAS']
            def keyPassword = props['KEY_PASSWORD']

            return [keystorePassword, keyAlias, keyPassword]
        } else {
            println 'signing.properties found but some entries are missing'
        }
    } else {
        println 'signing.properties not found'
    }
    return null
}

sonarRunner {
    sonarProperties {
        property "sonar.host.url", "http://sonar.fav24.net"
        property "sonar.jdbc.url",
          "jdbc:mysql://gm-ci-environment.cttjtyko7rpp.eu-west-1.rds.amazonaws.com:3306/sonar?useUnicode=true&amp;characterEncoding=utf8"
        property "sonar.jdbc.driverClassName", "com.mysql.jdbc.Driver"
        property "sonar.jdbc.username", "sonar"
        property "sonar.jdbc.password", "sonar"
        property "sonar.login", "admin"
        property "sonar.password", "14041981"
        property "sonar.projectName", "Shootr Android"
        property "sonar.projectVersion", android.defaultConfig.versionName
        properties["sonar.sources"] = android.sourceSets.main.java.srcDirs
    }
}

uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: "http://nexus.fav24.net/content/repositories/snapshots/") {
                authentication(userName: "deployment", password: "deployment123")
            }
            pom.version = android.defaultConfig.versionName
            pom.artifactId = 'Shootr'
            pom.groupId = "com.shooter.android"
            uniqueVersion = false
        }
    }
}